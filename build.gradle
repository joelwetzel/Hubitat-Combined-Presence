plugins {
    id 'groovy'
}

// Not using standard layout, need to specify folders.
sourceSets {
    test {
        groovy {
            srcDirs = ['tests', 'utils']
        }
    }
}

repositories {
    mavenCentral()

    // hubitat_ci repository
    maven {
        url uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY") ?: "biocomp/hubitat_ci"}")
        credentials {
            username System.getenv("GITHUB_ACTOR")
            password System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.4' // groovy itself
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.0'
    testImplementation "org.spockframework:spock-core:1.2-groovy-2.5" // spock framework
    testImplementation "me.biocomp.hubitat_ci:${System.getenv("MAVEN_ARTIFACT_ID") ?: 'hubitat_ci'}:0.49" // hubitat_ci (latest version)
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
}

test {
    doFirst {
        def hubitatCi = configurations.testRuntimeClasspath.resolvedConfiguration.resolvedArtifacts.find {
            it.moduleVersion.id.group == 'me.biocomp.hubitat_ci'
        }
        if (hubitatCi) {
            println "Using hubitat_ci version: ${hubitatCi.moduleVersion.id.version}"
        }
    }

    testLogging {
        showStandardStreams = true
    }
}
