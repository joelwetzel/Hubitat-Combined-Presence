plugins {
    id 'groovy'
}

// Not using standard layout, need to specify folders.
sourceSets {
    test {
        groovy {
            srcDirs = ['tests']
        }
    }
}

repositories {
    mavenCentral()

    // hubitat_ci repository
    maven {
        url uri("https://maven.pkg.github.com/${System.getenv("MAVEN_GITHUB_REPOSITORY") ?: "biocomp/hubitat_ci"}")
        credentials {
            username System.getenv("MAVEN_GITHUB_ACTOR")
            password System.getenv("MAVEN_GITHUB_TOKEN")
        }
    }
}

dependencies {
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.4' // groovy itself
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.5.0'
    testImplementation "org.spockframework:spock-core:1.2-groovy-2.5" // spock framework
    testImplementation "me.biocomp.hubitat_ci:${System.getenv("MAVEN_ARTIFACT_ID") ?: 'hubitat_ci'}:0.49" // hubitat_ci (latest version)
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '3.3.0'
}

test {
    doFirst {
        def hubitatCi = configurations.testRuntimeClasspath.resolvedConfiguration.resolvedArtifacts.find {
            it.moduleVersion.id.group == 'me.biocomp.hubitat_ci'
        }
        if (hubitatCi) {
            println "Using hubitat_ci version: ${hubitatCi.moduleVersion.id.version}"
        }
    }

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
        showStandardStreams = true
        
        // Print detailed failure information
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "\nTest Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
            }
        }
    }
}
